data <- readDiscreteCharacterData("data/placBRCA1third.nex")

n_species <- data.ntaxa()
n_branches <- 2 * n_species - 3
taxa <- data.taxa()

out_group = clade("Sorex")

# tree topology
topology ~ dnUniformTopology(taxa, outgroup = out_group) 
for (i in 1:n_branches) {
 bl[i] ~ dnExponential(10.0)
}
psi := treeAssembly(topology, bl)

# total tree length (just for monitoring)
TL := sum(bl)

# HKY substitution process
kappa ~ dnExponential(1.0)
pi ~ dnDirichlet([1.0, 1.0, 1.0, 1.0])
Q := fnHKY(kappa=kappa, baseFrequencies=nuc_freq)

seq ~ dnPhyloCTMC(tree=psi, Q=Q, type="DNA") 
seq.clamp(data)

my_model = model(psi)

# moves
moves = VectorMoves()
moves.append(mvNNI(topology, weight=3.0))
moves.append(mvSPR(topology, weight=3.0))
moves.append(mvScale(kappa, weight=1.0))
moves.append(mvDirichletSimplex(nuc_freq,weight=1.0, alpha=10))
for (i in 1:n_branches) {
   moves.append(mvScale(bl[i]))
}

monitors[1] = mnModel(filename="analyses/BRCA1thirdpos_HKY.log", printgen=10, separator = TAB)
monitors[2] = mnFile(psi,filename="analyses/BRCA1thirdpos_HKY.trees", printgen=10, separator = TAB)
monitors[3] = mnScreen(printgen=100, kappa, nuc_freq)

# Make the analysis object
analysis = mcmc(my_model, monitors, moves)
# Run the MCMC
analysis.run(10000)

# Post-analysis: constructing the maximum a posteriori tree
treetrace = readTreeTrace("analyses/BRCA1thirdpos_HKY.trees", treetype="non-clock", burnin=0.1)
map_tree = mapTree(treetrace, "analyses/BRCA1thirdpos_HKY_map.tree")


