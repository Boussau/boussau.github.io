### Read in alignment data for a single gene
# We read in data to get names for the tips of the tree, and a number of tips
# and sequence length for the simulation.
data = readDiscreteCharacterData("data/primates_and_galeopterus_cytb.nex")
# Get some useful variables from the data. We need these later on.
num_taxa <- data.ntaxa()
num_branches <- 2 * num_taxa - 3
taxa <- data.taxa()
#----------------------------#
## Why are we using "<-" and not ":=" or "~"?
#----------------------------#
######################
# Substitution Model #
######################
# create a constant variable for the (constant !) rate matrix
Q <- fnJC(4)
#----------------------------#
## What does the "4" stand for?
#----------------------------#
##############
# Tree model #
##############
# We choose an outgroup.
out_group = clade("Galeopterus_variegatus")
#----------------------------#
## Is this outgroup arbitrary?
#----------------------------#
# Prior distribution on the tree topology
topology ~ dnUniformTopology(taxa, outgroup=out_group)
#----------------------------#
## Why did we use "~" here?
#----------------------------#
# Branch length prior
for (i in 1:num_branches) {
    bl[i] ~ dnExponential(10.0)
    moves.append( mvScale(bl[i]) )
}
#----------------------------#
## What is the expected branch length according to this exponential prior?
#----------------------------#
TL := sum(bl)
#----------------------------#
## What is the meaning of this variable "TL"?
#----------------------------#
psi := treeAssembly(topology, bl)
#----------------------------#
## What purpose serves the function "treeAssembly"?
#----------------------------#
###################
# PhyloCTMC Model #
###################
# the sequence evolution model
seq ~ dnPhyloCTMC(tree=psi, Q=Q, type="DNA")
q()
