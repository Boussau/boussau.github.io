# read nucleotide data
data <- readDiscreteCharacterData("data/prim4fold.nex")

n_species <- data.ntaxa()
n_branches <- 2 * n_species - 2
taxa <- data.taxa()

# root age
root_age ~ dnUniform(0,100)
# tree is produced by a birth death process
# speciation rate
lambda ~ dnExponential(1.0)
# extinction rate
mu ~ dnExponential(1.0)
# sampling fraction (in the present)
# here, we assume that all species have been sampled
rho <- 1.0

# time tree produced by a birth-death process (BDP)
timetree ~ dnBDP(lambda, mu, rho, root_age, samplingStrategy="uniform", condition="nTaxa", taxa=taxa)

# read tree from file
tree <- readTrees("data/prim.tree", treetype="clock")[1]
# ajust terminal branch lengths so as to make the tree ultrametric
tree.makeUltrametric()

# set the value of the tree topology
timetree.setValue(tree)

# rescale root age to 1.0
# (all ages will be relative to the root)
root_age.setValue(1.0)

# we assume a strict molecular clock, of unknown rate
clockrate ~ dnExponential(1.0)

# and a T92 process
kappa ~ dnExponential(0.1)
gamma ~ dnBeta(1.0, 1.0)
Q := fnT92(kappa=kappa, gc=gamma)

seq ~ dnPhyloCTMC( tree=timetree, Q=Q, branchRates=clockrate, type="DNA" )
seq.clamp( data )

my_model = model(timetree)

# moves
moves = VectorMoves()

moves.append(mvScale(lambda, weight=1.0))
moves.append(mvScale(mu, weight=1.0))

moves.append(mvSlide(gamma, weight=1.0, delta=0.1))
moves.append(mvScale(kappa, weight=1.0))
moves.append(mvScale(clockrate, weight=1.0, lambda=0.1))

moves.append(mvNodeTimeSlideUniform(timetree, weight=20))
moves.append(mvSubtreeScale(timetree, weight=4))

monitors[1] = mnModel(filename="analyses/prim_clock.log", printgen=10, separator = TAB)
monitors[2] = mnFile(timetree ,filename="analyses/prim_clock.trees", printgen=10, separator = TAB)
monitors[3] = mnScreen(printgen=100, clockrate, kappa, gamma)

# Make the analysis object
analysis = mcmc(my_model, monitors, moves)
# Run the MCMC
analysis.run(10000)

treetrace = readTreeTrace("analyses/prim_clock.trees", treetype="clock", burnin=0.1)
map_tree = mccTree(treetrace, "analyses/prim_clock.tree")
