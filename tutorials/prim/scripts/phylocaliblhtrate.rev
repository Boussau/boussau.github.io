data <- readDiscreteCharacterData("data/simians4fold.nex")
contdata <- readContinuousCharacterData("data/primlht.nex")
maturity_index <- 1
longevity_index <- 3

n_species <- data.ntaxa()
n_branches <- 2 * n_species - 2
n_nodes <- 2 * n_species - 1
print(n_nodes)
taxa <- data.taxa()

root_age ~ dnUniform(0,100)

# tree is produced by a birth death process
lambda ~ dnExponential(1.0)
mu ~ dnExponential(1.0)
rho <- 1.0
timetree ~ dnBDP(lambda, mu, rho, root_age, samplingStrategy="uniform", condition="nTaxa", taxa=taxa)
tree <- readTrees("data/simians.tree", treetype="clock")[1]
tree.makeUltrametric()
timetree.clamp(tree)
#root_age.clamp(50)
#root_age.unclamp()

# brownian process
sigma_mat ~ dnExponential(1.0)
sigma_mat.clamp(0.1)
sigma_mat.unclamp()
nodelogmat[n_nodes] ~ dnNormal(mean=0, sd=20)
nodelogmat[n_nodes].clamp(5)
nodelogmat[n_nodes].unclamp()
for (i in (n_nodes-1):1)    {
    nodelogmat[i] ~ dnNormal(mean=nodelogmat[tree.parent(i)], sd=sigma_mat * sqrt(tree.branchLength(i)))
}

for (i in 1:n_species)  {
    if (contdata[contdata.taxonIndex(tree.nodeName(i))][maturity_index] != -1.0) {
        nodelogmat[i].clamp(log(abs(contdata[contdata.taxonIndex(tree.nodeName(i))][maturity_index]/365),exp(1.0)))
    }
}

# brownian process
sigma_lon ~ dnExponential(1.0)
sigma_lon.clamp(0.1)
sigma_lon.unclamp()
nodeloglon[n_nodes] ~ dnNormal(mean=0, sd=20)
nodeloglon[n_nodes].clamp(2)
nodeloglon[n_nodes].unclamp()
for (i in (n_nodes-1):1)    {
    nodeloglon[i] ~ dnNormal(mean=nodeloglon[tree.parent(i)], sd=sigma_lon * sqrt(tree.branchLength(i)))
}

for (i in 1:n_species)  {
    if (contdata[contdata.taxonIndex(tree.nodeName(i))][longevity_index] != -1.0) {
        nodeloglon[i].clamp(log(abs(contdata[contdata.taxonIndex(tree.nodeName(i))][longevity_index]),exp(1.0)))
    }
}

rate0 <- 6e-9
# beta = 3.3e-11 / 15 * 365
beta <- 8e-10
mixb <- 0.29

# print(rate0 + beta*mixb*120)
# print((rate0 + beta*mixb*120) / (4745/365 + mixb*120))

# rate across the tree
for (i in 1:n_branches) {
    clockrate[i] := abs(1e6 * 0.5 * (
        (rate0 + beta*mixb*exp(nodeloglon[i])) / (exp(nodelogmat[i]) + mixb*exp(nodeloglon[i])) + 
        (rate0 + beta*mixb*exp(nodeloglon[tree.parent(i)])) / (exp(nodelogmat[tree.parent(i)]) + mixb*exp(nodeloglon[tree.parent(i)])) ))
}

kappa ~ dnExponential(0.1)
gamma ~ dnBeta(1.0, 1.0)
Q := fnT92(kappa=kappa, gc=gamma)

seq ~ dnPhyloCTMC( tree=timetree, Q=Q, branchRates=clockrate, type="DNA" ) 
seq.clamp(data)

my_model = model(timetree)

# moves
moves = VectorMoves()

moves.append(mvScale(lambda, weight=1.0))
moves.append(mvScale(mu, weight=1.0))

moves.append(mvSlide(gamma, weight=1.0, delta=0.1))
moves.append(mvScale(kappa, weight=1.0))

moves.append(mvScale(sigma_mat, weight=1.0))
for (i in n_species:n_nodes) {
    moves.append(mvSlide(nodelogmat[i], weight=1.0))
}
for (i in 1:n_species)  {
    if (contdata[contdata.taxonIndex(tree.nodeName(i))][maturity_index] == -1.0) {
        moves.append(mvSlide(nodelogmat[i], weight=1.0))
    }
}

moves.append(mvScale(sigma_lon, weight=1.0))
for (i in n_species:n_nodes) {
    moves.append(mvSlide(nodeloglon[i], weight=1.0))
}
for (i in 1:n_species)  {
    if (contdata[contdata.taxonIndex(tree.nodeName(i))][longevity_index] == -1.0) {
        moves.append(mvSlide(nodeloglon[i], weight=1.0))
    }
}

moves.append(mvScale(root_age, weight=1.0))
moves.append(mvNodeTimeSlideUniform(timetree, weight=20))
moves.append(mvSubtreeScale(timetree, weight=4))

monitors[1] = mnModel(filename="analyses/simians_lhtrate.log", printgen=10, separator = TAB)
monitors[2] = mnFile(timetree ,filename="analyses/simians_lhtrate.trees", printgen=10, separator = TAB)
monitors[3] = mnScreen(printgen=100, root_age)

# Make the analysis object
analysis = mcmc(my_model, monitors, moves)
# Run the MCMC
analysis.run(10000)

treetrace = readTreeTrace("analyses/simians_lhtrate.trees", treetype="clock", burnin=0.1)
map_tree = mccTree(treetrace, "analyses/simians_lhtrate.tree")

