
data <- readDiscreteCharacterData("data/mamBRCA1.nex")
data_codon = data.translateCharacters("Codon")
print(data_codon)

n_species <- data.ntaxa()
n_branches <- 2 * n_species - 3
taxa <- data.taxa()

out_group = clade("Monodelphis")

topology ~ dnUniformTopology(taxa, outgroup = out_group) 
for (i in 1:n_branches) {
 bl[i] ~ dnExponential(10.0)
}
psi := treeAssembly(topology, bl)
TL := sum(bl)

kappa ~ dnExponential(0.1)
gamma ~ dnBeta(1.0, 1.0)
Q := fnT92(kappa=kappa, gc=gamma)

seq ~ dnPhyloCTMC( tree=psi, Q=Q, type="DNA" ) 
seq.clamp( data )

my_model = model(psi)

# moves
mvi <- 1
moves[1] = mvNNI(topology, weight=3.0)
moves[2] = mvSPR(topology, weight=3.0)
moves[3] = mvSlide(gamma, weight=1.0, delta=0.1)
moves[4] = mvScale(kappa, weight=1.0)
for (i in 1:n_branches) {
   moves[i+4] = mvScale(bl[i])
}

monitors[1] = mnModel(filename="analyses/mamBRCA1_T92.log", printgen=10, separator = TAB)
monitors[2] = mnFile(psi,filename="analyses/mamBRCA1_T92.trees", printgen=10, separator = TAB)
monitors[3] = mnScreen(printgen=100, kappa, gamma)

# Make the analysis object
analysis = mcmc(my_model, monitors, moves)
# Run the MCMC
analysis.run(10000)
# Show how the moves performed
analysis.operatorSummary()

