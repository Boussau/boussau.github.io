
data <- readDiscreteCharacterData("data/plac4fold.nex")
# data <- readDiscreteCharacterData("data/placZFX.nex")

n_branches <- 2 * data.ntaxa() - 3
taxa <- data.taxa()

out_group = clade("Sorex")

topology ~ dnUniformTopology(taxa, outgroup = out_group) 
for (i in 1:n_branches) {
 bl[i] ~ dnExponential(10.0)
}
psi := treeAssembly(topology, bl)
TL := sum(bl)

kappa ~ dnExponential(1.0)
gamma ~ dnBeta(1.0, 1.0)
Q := fnT92(kappa=kappa, gc=gamma)

# rates across sites
rate_var ~ dnExponential(1.0)
alpha := 1.0 / rate_var
ncat <- 4
disc_rates := fnDiscretizeGamma(alpha, alpha, 4)

seq ~ dnPhyloCTMC(tree=psi, Q=Q, siteRates=disc_rates, type="DNA" ) 
seq.clamp( data )

my_model = model(psi)

# moves
mvi <- 1
moves[1] = mvNNI(topology, weight=1.0)
moves[2] = mvSPR(topology, weight=1.0)
moves[3] = mvSlide(gamma, weight=1.0)
moves[4] = mvScale(kappa, weight=1.0)
moves[5] = mvScale(rate_var, weight=1.0)
for (i in 1:n_branches) {
   moves[i+5] = mvScale(bl[i])
}


monitors[1] = mnModel(filename="analyses/ZFX_T92ras.log", printgen=10, separator = TAB)
monitors[2] = mnFile(psi,filename="analyses/ZFX_T92ras.trees", printgen=10, separator = TAB)
monitors[3] = mnFile(rate_var,kappa,gamma,TL,filename="analyses/ZFX_T92ras.params", printgen=10, separator = TAB)
monitors[4] = mnScreen(printgen=100, TL)

# Make the analysis object
analysis = mcmc(my_model, monitors, moves)
# Run the MCMC
analysis.run(10000)
# Show how the moves performed
analysis.operatorSummary()

