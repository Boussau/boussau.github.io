data <- readDiscreteCharacterData("data/placBRCA1third.nex")

n_species <- data.ntaxa()
n_branches <- 2 * n_species - 3
taxa <- data.taxa()

out_group = clade("Sorex")

topology ~ dnUniformTopology(taxa, outgroup = out_group) 
for (i in 1:n_branches) {
 bl[i] ~ dnExponential(10.0)
}
psi := treeAssembly(topology, bl)
TL := sum(bl)

# kappa ~ dnHalfCauchy(location=0, scale = 1)
kappa ~ dnExponential(0.1)
gamma ~ dnBeta(1.0, 1.0)
Q := fnT92(kappa=kappa, gc=gamma)

seq ~ dnPhyloCTMC( tree=psi, Q=Q, type="DNA" ) 
seq.clamp( data )

my_model = model(psi)

# moves
mvi <- 1
moves[1] = mvNNI(topology, weight=3.0)
moves[2] = mvSPR(topology, weight=3.0)
moves[3] = mvSlide(gamma, weight=1.0)
moves[4] = mvScale(kappa, weight=1.0)
for (i in 1:n_branches) {
   moves[i+4] = mvScale(bl[i])
}

monitors[1] = mnModel(filename="analyses/BRCA13d_T92.log", printgen=10, separator = TAB)
monitors[2] = mnFile(psi,filename="analyses/BRCA13d_T92.trees", printgen=10, separator = TAB)
monitors[3] = mnScreen(printgen=100, TL)


# Make the analysis object
analysis = mcmc(my_model, monitors, moves, nruns=2)
# Run the MCMC
analysis.run(10000)

treetrace1 = readTreeTrace("analyses/BRCA13d_T92_run_1.trees", treetype="non-clock", burnin=0.1)
map_tree1 = mapTree(treetrace1, "analyses/BRCA13d_T92_run_1_map.tree")

treetrace2 = readTreeTrace("analyses/BRCA13d_T92_run_2.trees", treetype="non-clock", burnin=0.1)
map_tree2 = mapTree(treetrace2, "analyses/BRCA13d_T92_run_2_map.tree")

